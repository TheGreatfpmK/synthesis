// Simplified model of a mars rover
// Encoding by Tim Quatmann and Sebastian Junges
// RWTH Aachen University

mdp


const int num_tasks = 4;

// time (in minutes)
const int time_low = 5;
const int time_medium = 8;
const int time_high = 10;

// Energy (in percent)
const int energy_low = 3;
const int energy_medium = 5;
const int energy_high = 7; 

// Scientific Value
const int value_low = 2;
const int value_medium = 10;
const int value_high = 30;

// Success probabilities
const double task1_success_pr = 0.5;
const double task2_success_pr = 0.6;
const double task3_success_pr = 0.8;
const double task4_success_pr = 0.2;

formula low_time_task = (task=2 | task=3);
formula medium_time_task = false;
formula high_time_task = (task=1 | task=4);

formula low_energy_task = (task=1 | task=3);
formula medium_energy_task = (task=2);
formula high_energy_task = (task=4);

formula low_value_task = (task=3);
formula medium_value_task = (task=1 | task=2);
formula high_value_task = (task=4);

module rover
	// The current task (0 means no task)
	task : [0..num_tasks] init 0;
	success : bool init false;
	
	[task1_start] task=0 -> task1_success_pr : (task'=1) & (success'=true) + (1-task1_success_pr) : (task'=1) & (success'=false);
	[task2_start] task=0 -> task2_success_pr : (task'=2) & (success'=true) + (1-task2_success_pr) : (task'=2) & (success'=false);
	[task3_start] task=0 -> task3_success_pr : (task'=3) & (success'=true) + (1-task3_success_pr) : (task'=3) & (success'=false);
	[task4_start] task=0 -> task4_success_pr : (task'=4) & (success'=true) + (1-task4_success_pr) : (task'=4) & (success'=false);

	[task_done] task>0 -> (task'= 0) & (success'=false);
endmodule



module battery
	increased_energy : bool init false;
	
	[task1_start] true -> 0.5 : (increased_energy' = false) + (0.5) : (increased_energy' = true);
	[task2_start] true -> 0.5 : (increased_energy' = false) + (0.5) : (increased_energy' = true);
	[task3_start] true -> 0.5 : (increased_energy' = false) + (0.5) : (increased_energy' = true);
	[task4_start] true -> (increased_energy' = false);
	[task_done] true -> (increased_energy' = false);
endmodule
 

const int V;
module value_counting_module
	val : [0..V] init 0;
	[task_done] low_value_task -> (val'=min(V,val + (success ? value_low : 0)));
	[task_done] medium_value_task -> (val'=min(V,val + (success ? value_medium : 0)));
	[task_done] high_value_task -> (val'=min(V,val + (success ? value_high : 0)));
endmodule

rewards "time"
	[task_done] low_time_task : time_low;
	[task_done] medium_time_task : time_medium;
	[task_done] high_time_task : time_high;
endrewards

rewards "energy"
	[task_done] low_energy_task & !increased_energy : energy_low;
	[task_done] low_energy_task &  increased_energy : energy_medium;
	[task_done] medium_energy_task & !increased_energy : energy_medium;
	[task_done] medium_energy_task &  increased_energy : energy_high;
	[task_done] high_energy_task & !increased_energy : energy_high;
	[task_done] high_energy_task &  increased_energy : energy_high;
endrewards


label "goal" = val = V;

